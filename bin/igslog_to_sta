#!/usr/bin/perl 

use strict;

use File::Find;
use Getopt::Std;
use LINZ::BERN::StationFile;
use LINZ::BERN::CrdFile;
use LINZ::GNSS::IGSSiteLog;
use Carp;

my %opts;
getopts('vhnmdaPp:C:c:t:',\%opts);

help(0) if $opts{h};

my $crdfile=$opts{c} || $opts{C};
my $createsta=$opts{C};
my $addstn=$opts{a};
my $crdtol=$opts{t} // 10.0;
my $usename=$opts{n};
my $usedomes=$opts{d};
my $usenamecode=$opts{m};
my $logfilere=$opts{p} || ($opts{P} ? '{code}_{yyyy}{mm}{dd}.(log|xml)' : '');
my $verbose=$opts{v};

help(1) if @ARGV < ($createsta ? 2 :3) ;

my $outputsta=pop @ARGV;
my $inputsta=pop @ARGV if ! $createsta;
my @logfiles=@ARGV;


croak("Input Bernese station information file $inputsta does not exist") if $inputsta && ! -r $inputsta;
my $sta=new LINZ::BERN::StationFile($inputsta);

my $crds=0;
if( $crdfile )
{
    croak("Input Bernese coordinate file $crdfile does not exist") if ! -r $crdfile;
    $crds=new LINZ::BERN::CrdFile($crdfile);
}

# Compile logfile sets, each a list of candidate log files for a station.  The sets
# are lists of log files grouped by code (from the file name).  Each set holds the
# most recent entry from each directory in which the code is found. 
# Use this approach to handle codes being reused in different source data sets (assumed to be different 
# directories)).  Each set is a pair [logfilename,sortid] where the sortid is used to ensure we pick
# the most recent file in each directory.

my @logfilesets=();
if( $logfilere ne '')
{
    croak "Cannot specify more than one IGS log directory with -p option" if @logfiles > 1;
    my $logdir=$logfiles[0];
    my @parts=split(/(\*|\?|\{(?:code|yyyy|mm|dd|ddd)\}|\(\w+(?:\|\w+)*\))/,$logfilere);
    my $re='^';
    foreach my $p (@parts)
    {
        my $r = $p eq '*'      ? '.*' :
             $p eq '?'      ? '.' :
             $p eq '{code}' ? '(?<code>\w\w\w\w)' :
             $p eq '{yyyy}' ? '(?<year>(?:19|20)\d\d)' :
             $p eq '{mm}'   ? '(?<month>(?:0[1-9]|10|11|12))' :
             $p eq '{dd}'   ? '(?<day>(?:0[1-9]|[12]\d|30|31))' :
             $p eq '{doy}'  ? '(?<doy>(?:[0-3]\d\d))' :
             $p =~ /\((\w+(?:\|\w+)*\))/ ? '(?:'.$1 :
             quotemeta($p);
        $re .= $r;
    }
    $re .= '$';
    my $codes=();
    my $codedir;
    my $dircount;
    my @logdirs=split(/\:/,$logdir);
    foreach my $dir (@logdirs )
    {
        find({
            preprocess=>sort,
            wanted=>sub 
                {
                    my $filename=$_;
                    my $filepath=$File::Find::name;
                    my $filedir=$File::Find::dir;
                    return if $filename !~ /$re/;
                    my $code=uc($+{code});
                    if( $code )
                    {
                        # If using name codes then check that the code is defined in the .STA file.
                        if( $usenamecode )
                        {
                            my $rename=$sta->matchName($code);
                            if( ! $rename && $addstn && $crds )
                            {
                                $rename=1 if $crds->stationFromCode($code);
                            }
                            return if ! $rename;
                        }
                        # Pick the most recent log for a code in this directory
                        if( ! exists $codedir->{$code}->{$filedir} )
                        {
                            $dircount->{$code}++;
                            $codedir->{$code}->{$filedir}=$dircount->{$code};
                        }
                        my $ndir=$codedir->{$code}->{$filedir};
                        my $sortid=$+{yyyy}.$+{mm}.$+{dd}.$+{doy}.$filename;
                        my $lastid=$codes->{$code}->[$ndir]->[1];
                        $codes->{$code}->[$ndir]=[$filepath,$sortid] if $lastid eq '' || $lastid lt $sortid;
                    }
                    elsif( ! $usenamecode ) 
                    {
                        push(@logfiles,$filepath);
                    }
                }
            }, 
            $dir );
    }
    @logfilesets=values %$codes;
}
else
{
    foreach my $f (@logfiles)
    {
        croak("Log file $f does not exist\n") if ! -r $f;
        push(@logfilesets,[[$f,'']]);
    }
}
@logfiles=sort(@logfiles);

foreach my $logfileset (@logfilesets)
{
    my $used=0;
    foreach my $logfilep (@$logfileset)
    {
        last if $used;
        my $logfile=$logfilep->[0];
        print "Processing $logfile\n" if $verbose;
        my $sitelog=new LINZ::GNSS::IGSSiteLog($logfile);
        my $code=$sitelog->code;
        my $name=$code;
        if( $usedomes )
        {
            $name=$code;
            my $domes=$sitelog->domesNumber;
            $name .= ' '.$domes if $domes ne '';
        }
        elsif( $usename )
        {
            $name=$sitelog->name;
        }
        my $updates=$sta->loadIGSSiteLog($sitelog,name=>$name,update=>0,addnames=>$addstn);
        if( $crds )
        {
            my %names=();
            foreach my $d (@$updates)
            {
                $names{$d->{name}}=1;
            }
            my %remove=();
            my $xyz=$sitelog->approxXYZ;
            foreach my $name (keys %names)
            {
                my $stn=$crds->station($name);
                $stn=$crds->stationFromCode($code) if ! $stn;
                if( ! $stn )
                {
                    $remove{$name}=1;
                    print "Site log for $name ignored as not in coordinate file\n" if $verbose;
                    next;
                }
                my $testxyz=$stn->xyz;
                my $offset=sqrt(
                    ($xyz->[0]-$testxyz->[0])**2+
                    ($xyz->[1]-$testxyz->[1])**2+
                    ($xyz->[2]-$testxyz->[2])**2);
                if( $offset > $crdtol )
                {
                    $remove{$name}=1;
                    printf "Site log for $name ignored as outside coordinate tolerance (offset %.1f > %.1f)\n",$offset,$crdtol 
                        if $verbose;
                }
            }
            if( %remove )
            {
                my @using=();
                foreach my $d (@$updates)
                {
                    push(@using,$d) if ! $remove{$d->{name}};
                }
                $updates=\@using;
            }
        }
        if( scalar(@$updates) < 1 )
        {
            print "No updates taken from $logfile - names not matched\n" if $verbose;
            next;
        }
        $used=1;
        $sta->stationinfo($updates,1);
    }
}

$sta->mergeRenames() if $addstn;

my $tmpfile=$outputsta.'.tmp.'.$$;
eval
{
    $sta->write($tmpfile);
    unlink($outputsta);
    rename($tmpfile,$outputsta) || croak("Cannot create $outputsta\n");
    print "Created $outputsta\n" if $verbose;
};
if( $@ )
{
    my $error=$@;
    unlink($tmpfile);
    print $error;
}

sub testStationLocation
{
    my ($sitelog,$crds,$tolerance) = @_;

}

sub help
{
    my ($brief)=@_;
    require Pod::Usage;
    Pod::Usage::pod2usage(
    {
        -verbose=>$brief ? 99 : 2,
        -sections=>'Synopsis',
        -exitval=>2,
    });
}

__DATA__

=head1 Name

igslog_to_sta - Update Bernese station information from IGS site log

=head1 Synopsis

    igslog_to_sta [options] igslogfile ...  input_sta output_sta
    igslog_to_sta [options] [-p logfilepattern logdir ...] input_sta output_sta
    igslog_to_sta [options] [-C crdfile] [-p logfilepattern logdir ...] output_sta 
    igslog_to_sta -h

=head1 Description

Extract station information from one or more IGS site log files
and update the station information section (block 2) of a Bernese
station information file (.STA).

If a logfilepattern is specified then it can include replacement
strings {code} for station code and {yyyy} {mm} {dd} {ddd} for 
year, month, day of month, and day of year.  Where more than one
file is found for a station, the most recent one based on the filename
date will be used, otherwise the file sorting last alphabetically will
be used.  The search for log files will include the specified 
directory and all child directories. Once a log file matching a 
name (and optionally coordinate) is found then only files in that 
directory will be considered for that code.  Multiple search di
rectories can be specified either separated by ':' or as individual 
command line parameters.

(Note that the code is used 
only for file selection - the station code for matching the station
information file names comes from the contents of the log file. 
However see the -m option). 

Station names are matched either using the four character code,
possibly combined with the DOMES number, or the full name from the site
log. The names are matched using the station renaming specified 
in the station information file. 

A Bernese coordinate file can be specified, in which case the program 
will test the approximate coordinate in the site log against that in 
the coordinate file to confirm the match.  Stations which are outside 
a tolerance or not in the coordinate file will be ignored.

If the -u option is specified then only stations in the coordinate 
file are updated, otherwise all stations in the original file are
updated.  If an output coordinate file is specified then the output
will be written to that.

=head1 Options

Valid options are:

=over

=item -p filepattern

Defines a pattern for the filename.  Can include {code} for a 
station code, {yyyy}, {mm}, {dd}, or {ddd} for date components.
Also ? and * for a single character or arbitrary number of c
characters.  Results in a recursive search of the directory for
log files as described above.

When this parameter is used the script expects log directories
rather than log files on the command line. 

Generally this will be used with the -m option.

=item -P

Use default pattern {code}_{yyyy}{mm}{dd}.(xml|log)

=item -n 

Specify that the site log stations will be linked with the .STA stations
based on the name.

=item -d

Specify that the name will be compiled from the four character code and 
DOMES number (or just the four character code if the DOMES number is blank).

=item -m

Match codes with the filename.  Assumes the {code} pattern in the filename
matches the four character id, and only uses the file if this is 
identifiable in the station naming section (001) of the STA file.

=item -c crdfile

Specify the name of a Bernese .CRD file to use for confirming 
the IGS station name match based on the coordinate.

=item -C crdfile

Similar to -c except that there is no input station information
file - it is created directly from the log files.

=item -a 

Specify that logs matching any station code from the coordinate file
will be added even if they are not already in the station information
file.

=item -t tolerance

Specify the tolerance in metres used to confirm the name match.
The default is 10 metres.

=item -v

Verbose mode

=item -h

Prints help and exits

=back

=cut

